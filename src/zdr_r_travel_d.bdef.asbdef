managed;
strict ( 2 );
with draft;

define behavior for ZDR_R_TRAVEL_D //alias <alias_name>
implementation in class zbp_dr_travel_d unique
persistent table /dmo/a_travel_d
draft table zdr_travel_d
lock master
total etag LastChangedAt
authorization master ( instance, global )
etag master LocalLastChangedAt
{
  create ( precheck );
  update ( precheck );
  delete;
  field ( numbering : managed, readonly ) TravelUUID;
  field ( readonly ) TravelID, OverallStatus;
  association _Booking { create ( authorization : update ); with draft;}

  action ( authorization : update ) acceptTravel result [1] $self;
  action ( authorization : update ) rejectTravel result [1] $self;
  action ( authorization : update ) deductDiscount parameter ZDR_A_DEDUCT_DISCOUNT result [1] $self;
  internal action reCalcTotalPrice;

  determination SetTravelId on save { create; }
  determination SetStatusOpen on modify { create; }
  determination calcTotPrice on modify { create; field BookingFee, CurrencyCode; }

//  side effects { field BookingFee affects field TotalPrice; }

  field ( mandatory ) CustomerID;
  validation validateCustomer on save { create; field CustomerID; }

  draft action Resume with additional implementation;
  draft action Edit with additional implementation;
  draft action Activate optimized with additional implementation;
  draft action Discard with additional implementation;
  draft determine action Prepare
  {
  validation validateCustomer;
  validation ZDR_R_BOOKING_D~validateCustomer;
  validation ZDR_R_BOOKINGSUPPLEMENT_D~validateSupplement;
  }

  mapping for /dmo/a_travel_d
    {
      AgencyID           = agency_id;
      BeginDate          = begin_date;
      BookingFee         = booking_fee;
      CurrencyCode       = currency_code;
      CustomerID         = customer_id;
      Description        = description;
      EndDate            = end_date;
      LastChangedAt      = last_changed_at;
      LocalCreatedAt     = local_created_at;
      LocalCreatedBy     = local_created_by;
      LocalLastChangedAt = local_last_changed_at;
      LocalLastChangedBy = local_last_changed_by;
      OverallStatus      = overall_status;
      TotalPrice         = total_price;
      TravelID           = travel_id;
      TravelUUID         = travel_uuid;
    }
}

define behavior for ZDR_R_BOOKING_D //alias <alias_name>
implementation in class zbp_dr_booking_d unique
persistent table /dmo/a_booking_d
draft table zdr_booking_d
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( numbering : managed, readonly ) BookingUuid;
  field ( readonly ) TravelUUID, BookingId, BookingDate;
  association _Travel{with draft;}
  association _Bookingsupplement { create ( authorization : update );with draft; }

  determination SetBookingNumber on save { create; }
  determination SetBookingDate on save { create; }
  determination calculateTotalPrice on modify { create; field FlightPrice, CurrencyCode; }

//  side effects { field FlightPrice affects field _Travel.TotalPrice; }

  field ( mandatory ) CustomerID;

  validation validateCustomer on save { create; field CustomerID; }

  mapping for /dmo/a_booking_d
    {
      AirlineID          = carrier_id;
      BookingDate        = booking_date;
      BookingID          = booking_id;
      BookingStatus      = booking_status;
      BookingUUID        = booking_uuid;
      ConnectionID       = connection_id;
      CurrencyCode       = currency_code;
      CustomerID         = customer_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      LocalLastChangedAt = local_last_changed_at;
      TravelUUID         = parent_uuid;
    }
}

define behavior for ZDR_R_BOOKINGSUPPLEMENT_D //alias <alias_name>
implementation in class zbp_dr_bookingsupplement_d unique
persistent table /dmo/a_bksuppl_d
draft table zdr_bksuppl_d
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( numbering : managed, readonly ) BooksupplUuid;
  field ( readonly ) TravelUUID, BookingUuid, BookingSupplementId;
  association _Travel{ with draft; }
  association _Booking{ with draft; }

  determination SetBookSupplNumber on save { create; }
  determination calculateTotalPrice on modify { create; field Price, CurrencyCode; }

  validation validateSupplement on save { create; field supplementid; }

//  side effects { field Price affects field _Travel.TotalPrice; }

  mapping for /dmo/a_bksuppl_d
    {
      Price               = price;
      BookSupplUUID       = booksuppl_uuid;
      BookingSupplementID = booking_supplement_id;
      BookingUUID         = parent_uuid;
      CurrencyCode        = currency_code;
      LocalLastChangedAt  = local_last_changed_at;
      SupplementID        = supplement_id;
      TravelUUID          = root_uuid;
    }
}